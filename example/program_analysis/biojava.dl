// .in
.decl DirectSuperclass(class:int32, superclass:int32)
.input DirectSuperclass(IO="file", filename="DirectSuperclass.csv", delimiter=",")
.decl DirectSuperinterface(ref:int32, interface:int32)
.input DirectSuperinterface(IO="file", filename="DirectSuperinterface.csv", delimiter=",")
.decl MainClass(class:int32)
.input MainClass(IO="file", filename="MainClass.csv", delimiter=",")
.decl FormalParam(index:int32, method:int32, var:int32)
.input FormalParam(IO="file", filename="FormalParam.csv", delimiter=",")
.decl ComponentType(arrayType:int32, componentType:int32)
.input ComponentType(IO="file", filename="ComponentType.csv", delimiter=",")
.decl AssignReturnValue(invocation:int32, to:int32)
.input AssignReturnValue(IO="file", filename="AssignReturnValue.csv", delimiter=",")
.decl ActualParam(index:int32, invocation:int32, var:int32)
.input ActualParam(IO="file", filename="ActualParam.csv", delimiter=",")
.decl Method_Modifier(mod:int32, method:int32)
.input Method_Modifier(IO="file", filename="Method_Modifier.csv", delimiter=",")
.decl Var_Type(var:int32, type:int32)
.input Var_Type(IO="file", filename="Var_Type.csv", delimiter=",")
.decl HeapAllocation_Type(heap:int32, type:int32)
.input HeapAllocation_Type(IO="file", filename="HeapAllocation_Type.csv", delimiter=",") // modified to be an EDB now


// Main schema
// .decl Method_Descriptor(method:int32, descriptor:int32)
// .printsize
.decl isType(t:int32)
.decl isReferenceType(t:int32)
.decl isArrayType(t:int32)
.decl isClassType(t:int32)
.decl isInterfaceType(t:int32)
// .decl ApplicationClass(ref:int32)
.decl Field_DeclaringType(field:int32, declaringClass:int32)
.decl Method_DeclaringType(method:int32, declaringType:int32)
// .decl Method_ReturnType(method:int32, returnType:int32) // redundant
.decl Method_SimpleName(method:int32, simpleName:int32)
// .decl Method_Params(method:int32, params:int32) // redundant
.decl ThisVar(method:int32, var:int32)
// .decl Var_DeclaringMethod(var:int32, method:int32)
// .decl HeapAllocation_Type(heap:int32, type:int32) move into input
// .decl MainMethodArgArray(heap:int32) // redundant
// .decl MainMethodArgArrayContent(heap:int32) // redundant
.decl Instruction_Method(insn:int32, inMethod:int32)
.decl isVirtualMethodInvocation_Insn(insn:int32)
.decl isStaticMethodInvocation_Insn(insn:int32)
.decl FieldInstruction_Signature(insn:int32, sign:int32)
.decl LoadInstanceField_Base(insn:int32, var:int32)
.decl LoadInstanceField_To(insn:int32, var:int32)
.decl StoreInstanceField_From(insn:int32, var:int32)
.decl StoreInstanceField_Base(insn:int32, var:int32)
.decl LoadStaticField_To(insn:int32, var:int32)
.decl StoreStaticField_From(insn:int32, var:int32)
.decl LoadArrayIndex_Base(insn:int32, var:int32)
.decl LoadArrayIndex_To(insn:int32, var:int32)
.decl StoreArrayIndex_From(insn:int32, var:int32)
.decl StoreArrayIndex_Base(insn:int32, var:int32)
.decl AssignInstruction_To(insn:int32, to:int32)
.decl AssignCast_From(insn:int32, from:int32)
.decl AssignCast_Type(insn:int32, type:int32)
.decl AssignLocal_From(insn:int32, from:int32)
.decl AssignHeapAllocation_Heap(insn:int32, heap:int32)
.decl ReturnNonvoid_Var(return:int32, var:int32)
.decl MethodInvocation_Method(invocation:int32, signature:int32)
.decl VirtualMethodInvocation_Base(invocation:int32, base:int32)
.decl VirtualMethodInvocation_SimpleName(invocation:int32, simplename:int32)
.decl VirtualMethodInvocation_Descriptor(invocation:int32, descriptor:int32)
.decl SpecialMethodInvocation_Base(invocation:int32, base:int32)
// .decl MethodInvocation_Base(invocation:int32, base:int32)

// Fat schema
.decl LoadInstanceField(base:int32, sig:int32, to:int32, inmethod:int32)
.decl StoreInstanceField(from:int32, base:int32, signature:int32, inmethod:int32)
.decl LoadStaticField(sig:int32, to:int32, inmethod:int32)
.decl StoreStaticField(from:int32, signature:int32, inmethod:int32)
.decl LoadArrayIndex(base:int32, to:int32, inmethod:int32)
.decl StoreArrayIndex(from:int32, base:int32, inmethod:int32)
.decl AssignCast(type:int32, from:int32, to:int32, inmethod:int32)
.decl AssignLocal(from:int32, to:int32, inmethod:int32)
.decl AssignHeapAllocation(heap:int32, to:int32, inmethod:int32)
.decl ReturnVar(var:int32, method:int32)
.decl StaticMethodInvocation(invocation:int32, signature:int32, inmethod:int32)


// imports
// .in
.decl _ClassType(class:int32)
.input _ClassType(IO="file", filename="ClassType.csv", delimiter=",")
.decl _ArrayType(arrayType:int32)
.input _ArrayType(IO="file", filename="ArrayType.csv", delimiter=",")
.decl _InterfaceType(interface:int32)
.input _InterfaceType(IO="file", filename="InterfaceType.csv", delimiter=",")
// .decl _Var_DeclaringMethod(var:int32, method:int32)
// .input _Var_DeclaringMethod(IO="file", filename="Var_DeclaringMethod.csv", delimiter=",")
.decl _ApplicationClass(type:int32)
.input _ApplicationClass(IO="file", filename="ApplicationClass.csv", delimiter=",")
.decl _ThisVar(method:int32, var:int32)
.input _ThisVar(IO="file", filename="ThisVar.csv", delimiter=",")
.decl _NormalHeap(id:int32, type:int32)
.input _NormalHeap(IO="file", filename="NormalHeap.csv", delimiter=",")
// .decl _StringConstant(id:int32)
// .input _StringConstant(IO="file", filename="StringConstant.csv", delimiter=",")
.decl _AssignHeapAllocation(instruction:int32, index:int32, heap:int32, to:int32, inmethod:int32, linenumber:int32)
.input _AssignHeapAllocation(IO="file", filename="AssignHeapAllocation.csv", delimiter=",")
.decl _AssignLocal(instruction:int32, index:int32, from:int32, to:int32, inmethod:int32)
.input _AssignLocal(IO="file", filename="AssignLocal.csv", delimiter=",")
.decl _AssignCast(instruction:int32, index:int32, from:int32, to:int32, type:int32, inmethod:int32)
.input _AssignCast(IO="file", filename="AssignCast.csv", delimiter=",")
.decl _Field(signature:int32, declaringClass:int32, simplename:int32, type:int32)
.input _Field(IO="file", filename="Field.csv", delimiter=",")

// .rule
isType(class) :- _ClassType(class).
isReferenceType(class) :- _ClassType(class).
isClassType(class) :- _ClassType(class).

isType(arrayType) :- _ArrayType(arrayType).
isReferenceType(arrayType) :- _ArrayType(arrayType).
isArrayType(arrayType) :- _ArrayType(arrayType).

isType(interface) :- _InterfaceType(interface).
isReferenceType(interface) :- _InterfaceType(interface).
isInterfaceType(interface) :- _InterfaceType(interface).

// Var_DeclaringMethod(var, method) :- _Var_DeclaringMethod(var, method). // redundant Var_DeclaringMethod

isType(type) :- _ApplicationClass(type).
isReferenceType(type) :- _ApplicationClass(type).
// ApplicationClass(type) :- _ApplicationClass(type).

ThisVar(method, var) :- _ThisVar(method, var).

isType(type) :- _NormalHeap(_, type).
// HeapAllocation_Type(id, type) :- _NormalHeap(id, type).
// HeapAllocation_Type(id, 1127085) :- _StringConstant(id).

Instruction_Method(instruction, method) :-
  _AssignHeapAllocation(instruction, index, heap, to, method, linenumber).
AssignInstruction_To(instruction, to) :-
  _AssignHeapAllocation(instruction, index, heap, to, method, linenumber).
AssignHeapAllocation_Heap(instruction, heap) :-
  _AssignHeapAllocation(instruction, index, heap, to, method, linenumber).

Instruction_Method(instruction, method) :-
  _AssignLocal(instruction, index, from, to, method).
AssignLocal_From(instruction, from) :-
  _AssignLocal(instruction, index, from, to, method).
AssignInstruction_To(instruction, to) :- 
  _AssignLocal(instruction, index, from, to, method).

Instruction_Method(instruction, method) :-
  _AssignCast(instruction, index, from, to, type, method).
AssignCast_Type(instruction, type) :-
  _AssignCast(instruction, index, from, to, type, method).
AssignCast_From(instruction, from) :-
  _AssignCast(instruction, index, from, to, type, method).
AssignInstruction_To(instruction, to) :- 
  _AssignCast(instruction, index, from, to, type, method).

Field_DeclaringType(signature, declaringType) :- _Field(signature, declaringType, _, _).

// MethodInvocation_Base(invocation, base) :- VirtualMethodInvocation_Base(invocation, base).
// MethodInvocation_Base(invocation, base) :- SpecialMethodInvocation_Base(invocation, base).




// .in
.decl _StaticMethodInvocation(instruction:int32, index:int32, signature:int32, method:int32)
.input _StaticMethodInvocation(IO="file", filename="StaticMethodInvocation.csv", delimiter=",")
.decl _SpecialMethodInvocation(instruction:int32, index:int32, signature:int32, base:int32, method:int32)
.input _SpecialMethodInvocation(IO="file", filename="SpecialMethodInvocation.csv", delimiter=",")
.decl _VirtualMethodInvocation(instruction:int32, index:int32, signature:int32, base:int32, method:int32)
.input _VirtualMethodInvocation(IO="file", filename="VirtualMethodInvocation.csv", delimiter=",")
.decl _Method(method:int32, simplename:int32, params:int32, declaringType:int32, returnType:int32, jvmDescriptor:int32, arity:int32)
.input _Method(IO="file", filename="Method.csv", delimiter=",")
.decl Method_Descriptor(method:int32, descriptor:int32)
.input Method_Descriptor(IO="file", filename="Method_Descriptor.csv", delimiter=",")
.decl _StoreInstanceField(instruction:int32, index:int32, from:int32, base:int32, signature:int32, method:int32)
.input _StoreInstanceField(IO="file", filename="StoreInstanceField.csv", delimiter=",")
.decl _LoadInstanceField(instruction:int32, index:int32, to:int32, base:int32, signature:int32, method:int32)
.input _LoadInstanceField(IO="file", filename="LoadInstanceField.csv", delimiter=",")
.decl _StoreStaticField(instruction:int32, index:int32, from:int32, signature:int32, method:int32)
.input _StoreStaticField(IO="file", filename="StoreStaticField.csv", delimiter=",")
.decl _LoadStaticField(instruction:int32, index:int32, to:int32, signature:int32, method:int32)
.input _LoadStaticField(IO="file", filename="LoadStaticField.csv", delimiter=",")
.decl _StoreArrayIndex(instruction:int32, index:int32, from:int32, base:int32, method:int32)
.input _StoreArrayIndex(IO="file", filename="StoreArrayIndex.csv", delimiter=",")
.decl _LoadArrayIndex(instruction:int32, index:int32, to:int32, base:int32, method:int32)
.input _LoadArrayIndex(IO="file", filename="LoadArrayIndex.csv", delimiter=",")
.decl _Return(instruction:int32, index:int32, var:int32, method:int32)
.input _Return(IO="file", filename="Return.csv", delimiter=",")

// .rule
Instruction_Method(instruction, method) :-
  _StaticMethodInvocation(instruction, index, signature, method).
isStaticMethodInvocation_Insn(instruction) :-
  _StaticMethodInvocation(instruction, index, signature, method).
MethodInvocation_Method(instruction, signature) :- 
  _StaticMethodInvocation(instruction, index, signature, method).

Instruction_Method(instruction, method) :-
  _SpecialMethodInvocation(instruction, index, signature, base, method).
SpecialMethodInvocation_Base(instruction, base) :-
  _SpecialMethodInvocation(instruction, index, signature, base, method).
MethodInvocation_Method(instruction, signature) :-
  _SpecialMethodInvocation(instruction, index, signature, base, method).

Instruction_Method(instruction, method) :-
  _VirtualMethodInvocation(instruction, index, signature, base, method).
isVirtualMethodInvocation_Insn(instruction) :-
  _VirtualMethodInvocation(instruction, index, signature, base, method).
VirtualMethodInvocation_Base(instruction, base) :-
  _VirtualMethodInvocation(instruction, index, signature, base, method).
MethodInvocation_Method(instruction, signature) :-
  _VirtualMethodInvocation(instruction, index, signature, base, method).

Method_SimpleName(method, simplename) :-
  _Method(method, simplename, params, declaringType, returnType, jvmDescriptor, arity).
Method_DeclaringType(method, declaringType) :-
  _Method(method, simplename, params, declaringType, returnType, jvmDescriptor, arity).

// direct import of Method_Descriptor
// Method_Descriptor(method, descriptor) :- 
//   Method_ReturnType(method, returnType),
//   Method_Params(method, params),
//   descriptor = cat(returnType, params).

Instruction_Method(instruction, method) :-
  _StoreInstanceField(instruction, index, from, base, signature, method).
FieldInstruction_Signature(instruction, signature) :-
  _StoreInstanceField(instruction, index, from, base, signature, method).
StoreInstanceField_Base(instruction, base) :-
  _StoreInstanceField(instruction, index, from, base, signature, method).
StoreInstanceField_From(instruction, from) :-
  _StoreInstanceField(instruction, index, from, base, signature, method).

Instruction_Method(instruction, method) :-
  _LoadInstanceField(instruction, index, to, base, signature, method).
FieldInstruction_Signature(instruction, signature) :-
  _LoadInstanceField(instruction, index, to, base, signature, method).
LoadInstanceField_Base(instruction, base) :-
  _LoadInstanceField(instruction, index, to, base, signature, method).
LoadInstanceField_To(instruction, to) :-
  _LoadInstanceField(instruction, index, to, base, signature, method).

Instruction_Method(instruction, method) :-
  _StoreStaticField(instruction, index, from, signature, method).
FieldInstruction_Signature(instruction, signature) :-
  _StoreStaticField(instruction, index, from, signature, method).
StoreStaticField_From(instruction, from) :-
  _StoreStaticField(instruction, index, from, signature, method).

Instruction_Method(instruction, method) :-
  _LoadStaticField(instruction, index, to, signature, method).
FieldInstruction_Signature(instruction, signature) :-
  _LoadStaticField(instruction, index, to, signature, method).
LoadStaticField_To(instruction, to) :-
  _LoadStaticField(instruction, index, to, signature, method).

Instruction_Method(instruction, method) :-
  _StoreArrayIndex(instruction, index, from, base, method).
StoreArrayIndex_Base(instruction, base) :-
  _StoreArrayIndex(instruction, index, from, base, method).
StoreArrayIndex_From(instruction, from) :-
  _StoreArrayIndex(instruction, index, from, base, method).

Instruction_Method(instruction, method) :-
  _LoadArrayIndex(instruction, index, to, base, method).
LoadArrayIndex_Base(instruction, base) :-
  _LoadArrayIndex(instruction, index, to, base, method).
LoadArrayIndex_To(instruction, to) :-
  _LoadArrayIndex(instruction, index, to, base, method).

Instruction_Method(instruction, method) :-
  _Return(instruction, index, var, method).
ReturnNonvoid_Var(instruction, var) :-
  _Return(instruction, index, var, method).


// fat schema population
LoadInstanceField(base, sig, to, inmethod) :-
  Instruction_Method(insn, inmethod),
  LoadInstanceField_Base(insn, base),
  FieldInstruction_Signature(insn, sig),
  LoadInstanceField_To(insn, to).
StoreInstanceField(from, base, sig, inmethod) :-
  Instruction_Method(insn, inmethod),
  StoreInstanceField_From(insn, from),
  StoreInstanceField_Base(insn, base),
  FieldInstruction_Signature(insn, sig).
LoadStaticField(sig, to, inmethod) :-
  Instruction_Method(insn, inmethod),
  FieldInstruction_Signature(insn, sig),
  LoadStaticField_To(insn, to).
StoreStaticField(from, sig, inmethod) :-
  Instruction_Method(insn, inmethod),
  StoreStaticField_From(insn, from),
  FieldInstruction_Signature(insn, sig).
LoadArrayIndex(base, to, inmethod) :-
  Instruction_Method(insn, inmethod),
  LoadArrayIndex_Base(insn, base),
  LoadArrayIndex_To(insn, to).
StoreArrayIndex(from, base, inmethod) :-
  Instruction_Method(insn, inmethod),
  StoreArrayIndex_From(insn, from),
  StoreArrayIndex_Base(insn, base).
AssignCast(type, from, to, inmethod) :-
  Instruction_Method(insn, inmethod),
  AssignCast_From(insn, from),
  AssignInstruction_To(insn, to),
  AssignCast_Type(insn, type).
AssignLocal(from, to, inmethod) :-
  AssignInstruction_To(insn, to),
  Instruction_Method(insn, inmethod),
  AssignLocal_From(insn, from).
AssignHeapAllocation(heap, to, inmethod) :-
  Instruction_Method(insn, inmethod),
  AssignHeapAllocation_Heap(insn, heap),
  AssignInstruction_To(insn, to).
ReturnVar(var, method) :-
  Instruction_Method(insn, method),
  ReturnNonvoid_Var(insn, var).
StaticMethodInvocation(invocation, signature, inmethod) :-
  isStaticMethodInvocation_Insn(invocation),
  Instruction_Method(invocation, inmethod),
  MethodInvocation_Method(invocation, signature).
// HeapAllocation_Type(heap, type),
// MainMethodArgArray(heap) :-
//   heap = 4271541,
//   type = 922666.
// HeapAllocation_Type(heap, type),
// MainMethodArgArrayContent(heap) :-
//   heap = 4269990,
//   type = 1127085.
VirtualMethodInvocation_SimpleName(invocation, simplename) :-
  isVirtualMethodInvocation_Insn(invocation),
  MethodInvocation_Method(invocation, signature),
  Method_SimpleName(signature, simplename),
  Method_Descriptor(signature, descriptor).
VirtualMethodInvocation_Descriptor(invocation, descriptor) :-
  isVirtualMethodInvocation_Insn(invocation),
  MethodInvocation_Method(invocation, signature),
  Method_SimpleName(signature, simplename),
  Method_Descriptor(signature, descriptor).

  
// Basic (type-based) analysis
// .comp Basic {
// .printsize
.decl MethodLookup(simplename:int32, descriptor:int32, type:int32, method:int32)
.decl MethodImplemented(simplename:int32, descriptor:int32, type:int32, method:int32)
.decl DirectSubclass(a:int32, c:int32)
.decl Subclass(c:int32, a:int32)
.decl Superclass(c:int32, a:int32)
.decl Superinterface(k:int32, c:int32)
.decl SubtypeOf(subtype:int32, type:int32)
.decl SupertypeOf(supertype:int32, type:int32)
.decl SubtypeOfDifferent(subtype:int32, type:int32)
.decl MainMethodDeclaration(method:int32)

.printsize MethodLookup
.printsize MethodImplemented
.printsize DirectSubclass
.printsize Subclass
.printsize Superclass
.printsize Superinterface
.printsize SubtypeOf
.printsize SupertypeOf
.printsize SubtypeOfDifferent
.printsize MainMethodDeclaration

// .rule
MethodLookup(simplename, descriptor, type, method) :-
  MethodImplemented(simplename, descriptor, type, method).
MethodLookup(simplename, descriptor, type, method) :-
  DirectSuperclass(type, supertype),
  MethodLookup(simplename, descriptor, supertype, method),
  ! MethodImplemented(simplename, descriptor, type, _).
MethodLookup(simplename, descriptor, type, method) :-
  DirectSuperinterface(type, supertype),
  MethodLookup(simplename, descriptor, supertype, method),
  ! MethodImplemented(simplename, descriptor, type, _).
MethodImplemented(simplename, descriptor, type, method) :-
  Method_SimpleName(method, simplename),
  Method_Descriptor(method, descriptor),
  Method_DeclaringType(method, type),
  ! Method_Modifier(779558, method).
MainMethodDeclaration(method) :-
  MainClass(type),
  Method_DeclaringType(method, type),
  method != 1045470,
  method != 2357714,
  method != 1614718,
  Method_SimpleName(method, 780495),
  Method_Descriptor(method, 5097637),
  Method_Modifier(1347650, method),
  Method_Modifier(1347648, method).

DirectSubclass(a, c) :-
  DirectSuperclass(a, c).
Subclass(c, a) :-
  DirectSubclass(a, c).
Subclass(c, a) :-
  Subclass(b, a),
  DirectSubclass(b, c).
Superclass(c, a) :-
  Subclass(a, c).
Superinterface(k, c) :-
  DirectSuperinterface(c, k).
Superinterface(k, c) :-
  DirectSuperinterface(c, j),
  Superinterface(k, j).
Superinterface(k, c) :-
  DirectSuperclass(c, super),
  Superinterface(k, super).

SubtypeOf(s, s) :-
  isClassType(s).
SubtypeOf(t, t) :-
  isType(t).
SubtypeOf(s, t) :-
  Subclass(t, s).
SubtypeOf(s, s) :-
  isInterfaceType(s).
SubtypeOf(s, t) :-
  isClassType(s),
  Superinterface(t, s).
SubtypeOf(s, t) :-
  isInterfaceType(s),
  isType(t),
  t = 1168277.
SubtypeOf(s, t) :-
  isArrayType(s),
  isType(t),
  t = 1168277.
SubtypeOf(s, t) :-
  isInterfaceType(s),
  Superinterface(t, s).
// cross-product warning
// SubtypeOf(s, t) :-
//   ComponentType(s, sc),
//   ComponentType(t, tc),
//   isReferenceType(sc),
//   isReferenceType(tc),
//   SubtypeOf(sc, tc).
SubtypeOf(s, t) :-
  SubtypeOf(sc, tc),
  ComponentType(s, sc),
  ComponentType(t, tc),
  isReferenceType(sc),
  isReferenceType(tc).
SubtypeOf(s, t) :-
  isArrayType(s),
  isInterfaceType(t),
  isType(t),
  t = 1875761.
SubtypeOf(s, t) :-
  isArrayType(s),
  isInterfaceType(t),
  isType(t),
  t = 1132248.

SupertypeOf(s, t) :-
  SubtypeOf(t, s).
SubtypeOfDifferent(s, t) :-
  SubtypeOf(s, t),
  s != t.
// }
// .init basic = Basic





// class initialization
// .printsize
.decl ClassInitializer(type:int32, method:int32)
.decl InitializedClass(classOrInterface:int32)

.printsize ClassInitializer
.printsize InitializedClass

// .rule
ClassInitializer(type, method) :-
   MethodImplemented(1386348, 5096596, type, method).
InitializedClass(superclass) :-
   InitializedClass(class),
   DirectSuperclass(class, superclass).
InitializedClass(superinterface) :-
   InitializedClass(classOrInterface),
   DirectSuperinterface(classOrInterface, superinterface).
InitializedClass(class) :-
   MainMethodDeclaration(method),
   Method_DeclaringType(method, class).
InitializedClass(class) :-
   Reachable(inmethod),
   AssignHeapAllocation(heap, _, inmethod),
   HeapAllocation_Type(heap, class).
InitializedClass(class) :-
   Reachable(inmethod),
   Instruction_Method(invocation, inmethod),
   isStaticMethodInvocation_Insn(invocation),
   MethodInvocation_Method(invocation, signature),
   Method_DeclaringType(signature, class).
InitializedClass(classOrInterface) :-
   Reachable(inmethod),
   StoreStaticField(_, signature, inmethod),
   Field_DeclaringType(signature, classOrInterface).
InitializedClass(classOrInterface) :-
   Reachable(inmethod),
   LoadStaticField(signature, _, inmethod),
   Field_DeclaringType(signature, classOrInterface).
Reachable(clinit) :-
   InitializedClass(class),
   ClassInitializer(class, clinit).


// Main (value-based) analysis
// basic.MethodImplemented, basic.MainMethodDeclaration, basic.SupertypeOf, basic.MethodLookup
// .printsize
.decl Assign(to:int32, from:int32)
.decl VarPointsTo(heap:int32, var:int32)
.decl InstanceFieldPointsTo(heap:int32 , fld:int32, baseheap:int32)
.decl StaticFieldPointsTo(heap:int32, fld:int32)
.decl CallGraphEdge(invocation:int32, meth:int32)
.decl ArrayIndexPointsTo(baseheap:int32, heap:int32)
.decl Reachable(method:int32)

.printsize Assign
.printsize VarPointsTo
.printsize InstanceFieldPointsTo
.printsize StaticFieldPointsTo
.printsize CallGraphEdge
.printsize ArrayIndexPointsTo
.printsize Reachable

// .rule
Assign(actual, formal) :-
  CallGraphEdge(invocation, method),
  FormalParam(index, method, formal),
  ActualParam(index, invocation, actual).
Assign(return, local) :-
  CallGraphEdge(invocation, method),
  ReturnVar(return, method),
  AssignReturnValue(invocation, local).
VarPointsTo(heap, var) :-
  AssignHeapAllocation(heap, var, inMethod),
  Reachable(inMethod).
VarPointsTo(heap, to) :-
  Assign(from, to),
  VarPointsTo(heap, from).
VarPointsTo(heap, to) :-
  Reachable(inmethod),
  AssignLocal(from, to, inmethod),
  VarPointsTo(heap, from).
// .plan 1:(3,2,1)
VarPointsTo(heap, to) :-
  Reachable(inmethod),
  AssignCast(type, from, to, inmethod),
  SupertypeOf(type, heaptype),
  HeapAllocation_Type(heap, heaptype),
  VarPointsTo(heap, from).
//  .plan 1:(5,2,1,4,3)
ArrayIndexPointsTo(baseheap, heap) :-
  Reachable(inmethod),
  StoreArrayIndex(from, base, inmethod),
  VarPointsTo(baseheap, base),
  VarPointsTo(heap, from),
  HeapAllocation_Type(heap, heaptype),
  HeapAllocation_Type(baseheap, baseheaptype),
  ComponentType(baseheaptype, componenttype),
  SupertypeOf(componenttype, heaptype).
VarPointsTo(heap, to) :-
  Reachable(inmethod),
  LoadArrayIndex(base, to, inmethod),
  VarPointsTo(baseheap, base),
  ArrayIndexPointsTo(baseheap, heap),
  Var_Type(to, type),
  HeapAllocation_Type(baseheap, baseheaptype),
  ComponentType(baseheaptype, basecomponenttype),
  SupertypeOf(type, basecomponenttype).
// .plan 1:(3,2,1,4,5,6,7,8), 2:(4,3,2,1,5,6,7,8)
VarPointsTo(heap, to) :-
  Reachable(inmethod),
  LoadInstanceField(base, signature, to, inmethod),
  VarPointsTo(baseheap, base),
  InstanceFieldPointsTo(heap, signature, baseheap).
// .plan 1:(3,2,1,4), 2:(4,2,1,3)
VarPointsTo(heap, to) :-
  Reachable(inmethod),
  LoadStaticField(fld, to, inmethod),
  StaticFieldPointsTo(heap, fld).
VarPointsTo(heap, this) :-
  Reachable(inMethod),
  Instruction_Method(invocation, inMethod),
  VirtualMethodInvocation_Base(invocation, base),
  VarPointsTo(heap, base),
  HeapAllocation_Type(heap, heaptype),
  VirtualMethodInvocation_SimpleName(invocation, simplename),
  VirtualMethodInvocation_Descriptor(invocation, descriptor),
  MethodLookup(simplename, descriptor, heaptype, toMethod),
  ThisVar(toMethod, this).
// .plan 1:(4,3,2,1,5,6,7,8,9)
InstanceFieldPointsTo(heap, fld, baseheap) :-
  Reachable(inmethod),
  StoreInstanceField(from, base, fld, inmethod),
  VarPointsTo(heap, from),
  VarPointsTo(baseheap, base).
// .plan 1:(3,2,1,4), 2:(4,2,1,3)
StaticFieldPointsTo(heap, fld) :-
  Reachable(inmethod),
  StoreStaticField(from, fld, inmethod),
  VarPointsTo(heap, from).

// Reachable(toMethod), CallGraphEdge(invocation, toMethod)
Reachable(toMethod) :-
  Reachable(inMethod),
  Instruction_Method(invocation, inMethod),
  VirtualMethodInvocation_Base(invocation, base),
  VarPointsTo(heap, base),
  HeapAllocation_Type(heap, heaptype),
  VirtualMethodInvocation_SimpleName(invocation, simplename),
  VirtualMethodInvocation_Descriptor(invocation, descriptor),
  MethodLookup(simplename, descriptor, heaptype, toMethod).
CallGraphEdge(invocation, toMethod) :-
  Reachable(inMethod),
  Instruction_Method(invocation, inMethod),
  VirtualMethodInvocation_Base(invocation, base),
  VarPointsTo(heap, base),
  HeapAllocation_Type(heap, heaptype),
  VirtualMethodInvocation_SimpleName(invocation, simplename),
  VirtualMethodInvocation_Descriptor(invocation, descriptor),
  MethodLookup(simplename, descriptor, heaptype, toMethod).
// .plan 1:(4,3,2,1,5,6,7,8)

// Reachable(tomethod), CallGraphEdge(invocation, tomethod)
Reachable(tomethod) :-
  Reachable(inmethod),
  StaticMethodInvocation(invocation, tomethod, inmethod).
CallGraphEdge(invocation, tomethod) :-
  Reachable(inmethod),
  StaticMethodInvocation(invocation, tomethod, inmethod).

// Reachable(tomethod), CallGraphEdge(invocation, tomethod), VarPointsTo(heap, this)
Reachable(tomethod) :-
  Reachable(inmethod),
  Instruction_Method(invocation, inmethod),
  SpecialMethodInvocation_Base(invocation, base),
  VarPointsTo(heap, base),
  MethodInvocation_Method(invocation, tomethod),
  ThisVar(tomethod, this).
CallGraphEdge(invocation, tomethod) :-
  Reachable(inmethod),
  Instruction_Method(invocation, inmethod),
  SpecialMethodInvocation_Base(invocation, base),
  VarPointsTo(heap, base),
  MethodInvocation_Method(invocation, tomethod),
  ThisVar(tomethod, this).
VarPointsTo(heap, this) :-
  Reachable(inmethod),
  Instruction_Method(invocation, inmethod),
  SpecialMethodInvocation_Base(invocation, base),
  VarPointsTo(heap, base),
  MethodInvocation_Method(invocation, tomethod),
  ThisVar(tomethod, this).
// .plan 1:(4,3,2,1,5,6)

Reachable(method) :-
  MainMethodDeclaration(method).


// souffle -o doop ./doop.dl -j 16
// time ./doop -F./doop-data -D./idbs/ -j 16

