.in
.decl DirectSuperclass(class:integer, superclass:integer)
.input DirectSuperclass.csv

.decl DirectSuperinterface(ref:integer, interface:integer)
.input DirectSuperinterface.csv

.decl MainClass(class:integer)
.input MainClass.csv

.decl FormalParam(index:integer, method:integer, var:integer)
.input FormalParam.csv

.decl ComponentType(arrayType:integer, componentType:integer)
.input ComponentType.csv

.decl AssignReturnValue(invocation:integer, to:integer)
.input AssignReturnValue.csv

.decl ActualParam(index:integer, invocation:integer, var:integer)
.input ActualParam.csv

.decl Method_Modifier(mod:integer, method:integer)
.input Method_Modifier.csv
.decl Var_Type(var:integer, type:integer)
.input Var_Type.csv
.decl HeapAllocation_Type(heap:integer, type:integer)
.input HeapAllocation_Type.csv # modified to be an EDB now


# Main schema
# .decl Method_Descriptor(method:integer, descriptor:integer)
.out
.decl isType(t:integer)
.decl isReferenceType(t:integer)
.decl isArrayType(t:integer)
.decl isClassType(t:integer)
.decl isInterfaceType(t:integer)
.decl ApplicationClass(ref:integer)
.decl Field_DeclaringType(field:integer, declaringClass:integer)
.decl Method_DeclaringType(method:integer, declaringType:integer)
.decl Method_ReturnType(method:integer, returnType:integer)
.decl Method_SimpleName(method:integer, simpleName:integer)
.decl Method_Params(method:integer, params:integer)
.decl ThisVar(method:integer, var:integer)
.decl Var_DeclaringMethod(var:integer, method:integer)
# .decl HeapAllocation_Type(heap:integer, type:integer) move into input
# .decl MainMethodArgArray(heap:integer) redundant
# .decl MainMethodArgArrayContent(heap:integer) redundant
.decl Instruction_Method(insn:integer, inMethod:integer)
.decl isVirtualMethodInvocation_Insn(insn:integer)
.decl isStaticMethodInvocation_Insn(insn:integer)
.decl FieldInstruction_Signature(insn:integer, sign:integer)
.decl LoadInstanceField_Base(insn:integer, var:integer)
.decl LoadInstanceField_To(insn:integer, var:integer)
.decl StoreInstanceField_From(insn:integer, var:integer)
.decl StoreInstanceField_Base(insn:integer, var:integer)
.decl LoadStaticField_To(insn:integer, var:integer)
.decl StoreStaticField_From(insn:integer, var:integer)
.decl LoadArrayIndex_Base(insn:integer, var:integer)
.decl LoadArrayIndex_To(insn:integer, var:integer)
.decl StoreArrayIndex_From(insn:integer, var:integer)
.decl StoreArrayIndex_Base(insn:integer, var:integer)
.decl AssignInstruction_To(insn:integer, to:integer)
.decl AssignCast_From(insn:integer, from:integer)
.decl AssignCast_Type(insn:integer, type:integer)
.decl AssignLocal_From(insn:integer, from:integer)
.decl AssignHeapAllocation_Heap(insn:integer, heap:integer)
.decl ReturnNonvoid_Var(return:integer, var:integer)
.decl MethodInvocation_Method(invocation:integer, signature:integer)
.decl VirtualMethodInvocation_Base(invocation:integer, base:integer)
.decl VirtualMethodInvocation_SimpleName(invocation:integer, simplename:integer)
.decl VirtualMethodInvocation_Descriptor(invocation:integer, descriptor:integer)
.decl SpecialMethodInvocation_Base(invocation:integer, base:integer)
.decl MethodInvocation_Base(invocation:integer, base:integer)

# Fat schema
.decl LoadInstanceField(base:integer, sig:integer, to:integer, inmethod:integer)
.decl StoreInstanceField(from:integer, base:integer, signature:integer, inmethod:integer)
.decl LoadStaticField(sig:integer, to:integer, inmethod:integer)
.decl StoreStaticField(from:integer, signature:integer, inmethod:integer)
.decl LoadArrayIndex(base:integer, to:integer, inmethod:integer)
.decl StoreArrayIndex(from:integer, base:integer, inmethod:integer)
.decl AssignCast(type:integer, from:integer, to:integer, inmethod:integer)
.decl AssignLocal(from:integer, to:integer, inmethod:integer)
.decl AssignHeapAllocation(heap:integer, to:integer, inmethod:integer)
.decl ReturnVar(var:integer, method:integer)
.decl StaticMethodInvocation(invocation:integer, signature:integer, inmethod:integer)


# imports
.in
.decl _ClassType(class:integer)
.input ClassType.csv
.decl _ArrayType(arrayType:integer)
.input ArrayType.csv
.decl _InterfaceType(interface:integer)
.input InterfaceType.csv
.decl _Var_DeclaringMethod(var:integer, method:integer)
.input Var_DeclaringMethod.csv 
.decl _ApplicationClass(type:integer)
.input ApplicationClass.csv
.decl _ThisVar(method:integer, var:integer)
.input ThisVar.csv
.decl _NormalHeap(id:integer, type:integer)
.input NormalHeap.csv
.decl _StringConstant(id:integer)
.input StringConstant.csv
.decl _AssignHeapAllocation(instruction:integer, index:integer, heap:integer, to:integer, inmethod:integer, linenumber:integer)
.input AssignHeapAllocation.csv
.decl _AssignLocal(instruction:integer, index:integer, from:integer, to:integer, inmethod:integer)
.input AssignLocal.csv
.decl _AssignCast(instruction:integer, index:integer, from:integer, to:integer, type:integer, inmethod:integer)
.input AssignCast.csv
.decl _Field(signature:integer, declaringClass:integer, simplename:integer, type:integer)
.input Field.csv

.rule
isType(class) :- _ClassType(class).
isReferenceType(class) :- _ClassType(class).
isClassType(class) :- _ClassType(class).

isType(arrayType) :- _ArrayType(arrayType).
isReferenceType(arrayType) :- _ArrayType(arrayType).
isArrayType(arrayType) :- _ArrayType(arrayType).

isType(interface) :- _InterfaceType(interface).
isReferenceType(interface) :- _InterfaceType(interface).
isInterfaceType(interface) :- _InterfaceType(interface).

Var_DeclaringMethod(var, method) :- _Var_DeclaringMethod(var, method). # redundant Var_DeclaringMethod

isType(type) :- _ApplicationClass(type).
isReferenceType(type) :- _ApplicationClass(type).
ApplicationClass(type) :- _ApplicationClass(type).

ThisVar(method, var) :- _ThisVar(method, var).

isType(type) :- _NormalHeap(_, type).
# HeapAllocation_Type(id, type) :- _NormalHeap(id, type).
# HeapAllocation_Type(id, 371490) :- _StringConstant(id).

Instruction_Method(instruction, method) :-
  _AssignHeapAllocation(instruction, index, heap, to, method, linenumber).
AssignInstruction_To(instruction, to) :-
  _AssignHeapAllocation(instruction, index, heap, to, method, linenumber).
AssignHeapAllocation_Heap(instruction, heap) :-
  _AssignHeapAllocation(instruction, index, heap, to, method, linenumber).

Instruction_Method(instruction, method) :-
  _AssignLocal(instruction, index, from, to, method).
AssignLocal_From(instruction, from) :-
  _AssignLocal(instruction, index, from, to, method).
AssignInstruction_To(instruction, to) :- 
  _AssignLocal(instruction, index, from, to, method).

Instruction_Method(instruction, method) :-
  _AssignCast(instruction, index, from, to, type, method).
AssignCast_Type(instruction, type) :-
  _AssignCast(instruction, index, from, to, type, method).
AssignCast_From(instruction, from) :-
  _AssignCast(instruction, index, from, to, type, method).
AssignInstruction_To(instruction, to) :- 
  _AssignCast(instruction, index, from, to, type, method).

Field_DeclaringType(signature, declaringType) :- _Field(signature, declaringType, _, _).

MethodInvocation_Base(invocation, base) :- VirtualMethodInvocation_Base(invocation, base).
MethodInvocation_Base(invocation, base) :- SpecialMethodInvocation_Base(invocation, base).




.in
.decl _StaticMethodInvocation(instruction:integer, index:integer, signature:integer, method:integer)
.input StaticMethodInvocation.csv
.decl _SpecialMethodInvocation(instruction:integer, index:integer, signature:integer, base:integer, method:integer)
.input SpecialMethodInvocation.csv
.decl _VirtualMethodInvocation(instruction:integer, index:integer, signature:integer, base:integer, method:integer)
.input VirtualMethodInvocation.csv
.decl _Method(method:integer, simplename:integer, params:integer, declaringType:integer, returnType:integer, jvmDescriptor:integer, arity:integer)
.input Method.csv
.decl Method_Descriptor(method:integer, descriptor:integer)
.input Method_Descriptor.csv
.decl _StoreInstanceField(instruction:integer, index:integer, from:integer, base:integer, signature:integer, method:integer)
.input StoreInstanceField.csv
.decl _LoadInstanceField(instruction:integer, index:integer, to:integer, base:integer, signature:integer, method:integer)
.input LoadInstanceField.csv
.decl _StoreStaticField(instruction:integer, index:integer, from:integer, signature:integer, method:integer)
.input StoreStaticField.csv
.decl _LoadStaticField(instruction:integer, index:integer, to:integer, signature:integer, method:integer)
.input LoadStaticField.csv
.decl _StoreArrayIndex(instruction:integer, index:integer, from:integer, base:integer, method:integer)
.input StoreArrayIndex.csv
.decl _LoadArrayIndex(instruction:integer, index:integer, to:integer, base:integer, method:integer)
.input LoadArrayIndex.csv
.decl _Return(instruction:integer, index:integer, var:integer, method:integer)
.input Return.csv

.rule
Instruction_Method(instruction, method) :-
  _StaticMethodInvocation(instruction, index, signature, method).
isStaticMethodInvocation_Insn(instruction) :-
  _StaticMethodInvocation(instruction, index, signature, method).
MethodInvocation_Method(instruction, signature) :- 
  _StaticMethodInvocation(instruction, index, signature, method).

Instruction_Method(instruction, method) :-
  _SpecialMethodInvocation(instruction, index, signature, base, method).
SpecialMethodInvocation_Base(instruction, base) :-
  _SpecialMethodInvocation(instruction, index, signature, base, method).
MethodInvocation_Method(instruction, signature) :-
  _SpecialMethodInvocation(instruction, index, signature, base, method).

Instruction_Method(instruction, method) :-
  _VirtualMethodInvocation(instruction, index, signature, base, method).
isVirtualMethodInvocation_Insn(instruction) :-
  _VirtualMethodInvocation(instruction, index, signature, base, method).
VirtualMethodInvocation_Base(instruction, base) :-
  _VirtualMethodInvocation(instruction, index, signature, base, method).
MethodInvocation_Method(instruction, signature) :-
  _VirtualMethodInvocation(instruction, index, signature, base, method).

Method_SimpleName(method, simplename) :-
  _Method(method, simplename, params, declaringType, returnType, jvmDescriptor, arity).
Method_DeclaringType(method, declaringType) :-
  _Method(method, simplename, params, declaringType, returnType, jvmDescriptor, arity).

# direct import of Method_Descriptor
# Method_Descriptor(method, descriptor) :- 
#   Method_ReturnType(method, returnType),
#   Method_Params(method, params),
#   descriptor = cat(returnType, params).

Instruction_Method(instruction, method) :-
  _StoreInstanceField(instruction, index, from, base, signature, method).
FieldInstruction_Signature(instruction, signature) :-
  _StoreInstanceField(instruction, index, from, base, signature, method).
StoreInstanceField_Base(instruction, base) :-
  _StoreInstanceField(instruction, index, from, base, signature, method).
StoreInstanceField_From(instruction, from) :-
  _StoreInstanceField(instruction, index, from, base, signature, method).

Instruction_Method(instruction, method) :-
  _LoadInstanceField(instruction, index, to, base, signature, method).
FieldInstruction_Signature(instruction, signature) :-
  _LoadInstanceField(instruction, index, to, base, signature, method).
LoadInstanceField_Base(instruction, base) :-
  _LoadInstanceField(instruction, index, to, base, signature, method).
LoadInstanceField_To(instruction, to) :-
  _LoadInstanceField(instruction, index, to, base, signature, method).

Instruction_Method(instruction, method) :-
  _StoreStaticField(instruction, index, from, signature, method).
FieldInstruction_Signature(instruction, signature) :-
  _StoreStaticField(instruction, index, from, signature, method).
StoreStaticField_From(instruction, from) :-
  _StoreStaticField(instruction, index, from, signature, method).

Instruction_Method(instruction, method) :-
  _LoadStaticField(instruction, index, to, signature, method).
FieldInstruction_Signature(instruction, signature) :-
  _LoadStaticField(instruction, index, to, signature, method).
LoadStaticField_To(instruction, to) :-
  _LoadStaticField(instruction, index, to, signature, method).

Instruction_Method(instruction, method) :-
  _StoreArrayIndex(instruction, index, from, base, method).
StoreArrayIndex_Base(instruction, base) :-
  _StoreArrayIndex(instruction, index, from, base, method).
StoreArrayIndex_From(instruction, from) :-
  _StoreArrayIndex(instruction, index, from, base, method).

Instruction_Method(instruction, method) :-
  _LoadArrayIndex(instruction, index, to, base, method).
LoadArrayIndex_Base(instruction, base) :-
  _LoadArrayIndex(instruction, index, to, base, method).
LoadArrayIndex_To(instruction, to) :-
  _LoadArrayIndex(instruction, index, to, base, method).

Instruction_Method(instruction, method) :-
  _Return(instruction, index, var, method).
ReturnNonvoid_Var(instruction, var) :-
  _Return(instruction, index, var, method).


# fat schema population
LoadInstanceField(base, sig, to, inmethod) :-
  Instruction_Method(insn, inmethod),
  LoadInstanceField_Base(insn, base),
  FieldInstruction_Signature(insn, sig),
  LoadInstanceField_To(insn, to).
StoreInstanceField(from, base, sig, inmethod) :-
  Instruction_Method(insn, inmethod),
  StoreInstanceField_From(insn, from),
  StoreInstanceField_Base(insn, base),
  FieldInstruction_Signature(insn, sig).
LoadStaticField(sig, to, inmethod) :-
  Instruction_Method(insn, inmethod),
  FieldInstruction_Signature(insn, sig),
  LoadStaticField_To(insn, to).
StoreStaticField(from, sig, inmethod) :-
  Instruction_Method(insn, inmethod),
  StoreStaticField_From(insn, from),
  FieldInstruction_Signature(insn, sig).
LoadArrayIndex(base, to, inmethod) :-
  Instruction_Method(insn, inmethod),
  LoadArrayIndex_Base(insn, base),
  LoadArrayIndex_To(insn, to).
StoreArrayIndex(from, base, inmethod) :-
  Instruction_Method(insn, inmethod),
  StoreArrayIndex_From(insn, from),
  StoreArrayIndex_Base(insn, base).
AssignCast(type, from, to, inmethod) :-
  Instruction_Method(insn, inmethod),
  AssignCast_From(insn, from),
  AssignInstruction_To(insn, to),
  AssignCast_Type(insn, type).
AssignLocal(from, to, inmethod) :-
  AssignInstruction_To(insn, to),
  Instruction_Method(insn, inmethod),
  AssignLocal_From(insn, from).
AssignHeapAllocation(heap, to, inmethod) :-
  Instruction_Method(insn, inmethod),
  AssignHeapAllocation_Heap(insn, heap),
  AssignInstruction_To(insn, to).
ReturnVar(var, method) :-
  Instruction_Method(insn, method),
  ReturnNonvoid_Var(insn, var).
StaticMethodInvocation(invocation, signature, inmethod) :-
  isStaticMethodInvocation_Insn(invocation),
  Instruction_Method(invocation, inmethod),
  MethodInvocation_Method(invocation, signature).
# HeapAllocation_Type(heap, type),
# MainMethodArgArray(heap) :-
#   heap = 1323624,
#   type = 278848.
# HeapAllocation_Type(heap, type),
# MainMethodArgArrayContent(heap) :-
#   heap = 1323210,
#   type = 371490.
VirtualMethodInvocation_SimpleName(invocation, simplename) :-
  isVirtualMethodInvocation_Insn(invocation),
  MethodInvocation_Method(invocation, signature),
  Method_SimpleName(signature, simplename),
  Method_Descriptor(signature, descriptor).
VirtualMethodInvocation_Descriptor(invocation, descriptor) :-
  isVirtualMethodInvocation_Insn(invocation),
  MethodInvocation_Method(invocation, signature),
  Method_SimpleName(signature, simplename),
  Method_Descriptor(signature, descriptor).

  
# Basic (type-based) analysis
# .comp Basic {
.out
.decl MethodLookup(simplename:integer, descriptor:integer, type:integer, method:integer)
.decl MethodImplemented(simplename:integer, descriptor:integer, type:integer, method:integer)
.decl DirectSubclass(a:integer, c:integer)
.decl Subclass(c:integer, a:integer)
.decl Superclass(c:integer, a:integer)
.decl Superinterface(k:integer, c:integer)
.decl SubtypeOf(subtype:integer, type:integer)
.decl SupertypeOf(supertype:integer, type:integer)
.decl SubtypeOfDifferent(subtype:integer, type:integer)
.decl MainMethodDeclaration(method:integer)












.rule
MethodLookup(simplename, descriptor, type, method) :-
  MethodImplemented(simplename, descriptor, type, method).
MethodLookup(simplename, descriptor, type, method) :-
  DirectSuperclass(type, supertype),
  MethodLookup(simplename, descriptor, supertype, method),
  ! MethodImplemented(simplename, descriptor, type, _).
MethodLookup(simplename, descriptor, type, method) :-
  DirectSuperinterface(type, supertype),
  MethodLookup(simplename, descriptor, supertype, method),
  ! MethodImplemented(simplename, descriptor, type, _).
MethodImplemented(simplename, descriptor, type, method) :-
  Method_SimpleName(method, simplename),
  Method_Descriptor(method, descriptor),
  Method_DeclaringType(method, type),
  ! Method_Modifier(1123159, method).
MainMethodDeclaration(method) :-
  MainClass(type),
  Method_DeclaringType(method, type),
  method != 486241,
  Method_SimpleName(method, 1552149),
  Method_Descriptor(method, 1577746),
  Method_Modifier(233753, method),
  Method_Modifier(434846, method).

DirectSubclass(a, c) :-
  DirectSuperclass(a, c).
Subclass(c, a) :-
  DirectSubclass(a, c).
Subclass(c, a) :-
  Subclass(b, a),
  DirectSubclass(b, c).
Superclass(c, a) :-
  Subclass(a, c).
Superinterface(k, c) :-
  DirectSuperinterface(c, k).
Superinterface(k, c) :-
  DirectSuperinterface(c, j),
  Superinterface(k, j).
Superinterface(k, c) :-
  DirectSuperclass(c, super),
  Superinterface(k, super).

SubtypeOf(s, s) :-
  isClassType(s).
SubtypeOf(t, t) :-
  isType(t).
SubtypeOf(s, t) :-
  Subclass(t, s).
SubtypeOf(s, s) :-
  isInterfaceType(s).
SubtypeOf(s, t) :-
  isClassType(s),
  Superinterface(t, s).
SubtypeOf(s, t) :-
  isInterfaceType(s),
  isType(t),
  t = 233751.
SubtypeOf(s, t) :-
  isArrayType(s),
  isType(t),
  t = 233751.
SubtypeOf(s, t) :-
  isInterfaceType(s),
  Superinterface(t, s).
# cross-product warning
# SubtypeOf(s, t) :-
#   ComponentType(s, sc),
#   ComponentType(t, tc),
#   isReferenceType(sc),
#   isReferenceType(tc),
#   SubtypeOf(sc, tc).
SubtypeOf(s, t) :-
  SubtypeOf(sc, tc),
  ComponentType(s, sc),
  ComponentType(t, tc),
  isReferenceType(sc),
  isReferenceType(tc).
SubtypeOf(s, t) :-
  isArrayType(s),
  isInterfaceType(t),
  isType(t),
  t = 573394.
SubtypeOf(s, t) :-
  isArrayType(s),
  isInterfaceType(t),
  isType(t),
  t = 375747.

SupertypeOf(s, t) :-
  SubtypeOf(t, s).
SubtypeOfDifferent(s, t) :-
  SubtypeOf(s, t),
  s != t.
# }
# .init basic = Basic





# class initialization
.out
.decl ClassInitializer(type:integer, method:integer)
.decl InitializedClass(classOrInterface:integer)




.rule
ClassInitializer(type, method) :-
   MethodImplemented(443718, 1577013, type, method).
InitializedClass(superclass) :-
   InitializedClass(class),
   DirectSuperclass(class, superclass).
InitializedClass(superinterface) :-
   InitializedClass(classOrInterface),
   DirectSuperinterface(classOrInterface, superinterface).
InitializedClass(class) :-
   MainMethodDeclaration(method),
   Method_DeclaringType(method, class).
InitializedClass(class) :-
   Reachable(inmethod),
   AssignHeapAllocation(heap, _, inmethod),
   HeapAllocation_Type(heap, class).
InitializedClass(class) :-
   Reachable(inmethod),
   Instruction_Method(invocation, inmethod),
   isStaticMethodInvocation_Insn(invocation),
   MethodInvocation_Method(invocation, signature),
   Method_DeclaringType(signature, class).
InitializedClass(classOrInterface) :-
   Reachable(inmethod),
   StoreStaticField(_, signature, inmethod),
   Field_DeclaringType(signature, classOrInterface).
InitializedClass(classOrInterface) :-
   Reachable(inmethod),
   LoadStaticField(signature, _, inmethod),
   Field_DeclaringType(signature, classOrInterface).
Reachable(clinit) :-
   InitializedClass(class),
   ClassInitializer(class, clinit).


# Main (value-based) analysis
# basic.MethodImplemented, basic.MainMethodDeclaration, basic.SupertypeOf, basic.MethodLookup
.out
.decl Assign(to:integer, from:integer)
.decl VarPointsTo(heap:integer, var:integer)
.decl InstanceFieldPointsTo(heap:integer , fld:integer, baseheap:integer)
.decl StaticFieldPointsTo(heap:integer, fld:integer)
.decl CallGraphEdge(invocation:integer, meth:integer)
.decl ArrayIndexPointsTo(baseheap:integer, heap:integer)
.decl Reachable(method:integer)









.rule
Assign(actual, formal) :-
  CallGraphEdge(invocation, method),
  FormalParam(index, method, formal),
  ActualParam(index, invocation, actual).
Assign(return, local) :-
  CallGraphEdge(invocation, method),
  ReturnVar(return, method),
  AssignReturnValue(invocation, local).
VarPointsTo(heap, var) :-
  AssignHeapAllocation(heap, var, inMethod),
  Reachable(inMethod).
VarPointsTo(heap, to) :-
  Assign(from, to),
  VarPointsTo(heap, from).
VarPointsTo(heap, to) :-
  Reachable(inmethod),
  AssignLocal(from, to, inmethod),
  VarPointsTo(heap, from).
# .plan 1:(3,2,1)
VarPointsTo(heap, to) :-
  Reachable(inmethod),
  AssignCast(type, from, to, inmethod),
  SupertypeOf(type, heaptype),
  HeapAllocation_Type(heap, heaptype),
  VarPointsTo(heap, from).
#  .plan 1:(5,2,1,4,3)
ArrayIndexPointsTo(baseheap, heap) :-
  Reachable(inmethod),
  StoreArrayIndex(from, base, inmethod),
  VarPointsTo(baseheap, base),
  VarPointsTo(heap, from),
  HeapAllocation_Type(heap, heaptype),
  HeapAllocation_Type(baseheap, baseheaptype),
  ComponentType(baseheaptype, componenttype),
  SupertypeOf(componenttype, heaptype).
VarPointsTo(heap, to) :-
  Reachable(inmethod),
  LoadArrayIndex(base, to, inmethod),
  VarPointsTo(baseheap, base),
  ArrayIndexPointsTo(baseheap, heap),
  Var_Type(to, type),
  HeapAllocation_Type(baseheap, baseheaptype),
  ComponentType(baseheaptype, basecomponenttype),
  SupertypeOf(type, basecomponenttype).
# .plan 1:(3,2,1,4,5,6,7,8), 2:(4,3,2,1,5,6,7,8)
VarPointsTo(heap, to) :-
  Reachable(inmethod),
  LoadInstanceField(base, signature, to, inmethod),
  VarPointsTo(baseheap, base),
  InstanceFieldPointsTo(heap, signature, baseheap).
# .plan 1:(3,2,1,4), 2:(4,2,1,3)
VarPointsTo(heap, to) :-
  Reachable(inmethod),
  LoadStaticField(fld, to, inmethod),
  StaticFieldPointsTo(heap, fld).
VarPointsTo(heap, this) :-
  Reachable(inMethod),
  Instruction_Method(invocation, inMethod),
  VirtualMethodInvocation_Base(invocation, base),
  VarPointsTo(heap, base),
  HeapAllocation_Type(heap, heaptype),
  VirtualMethodInvocation_SimpleName(invocation, simplename),
  VirtualMethodInvocation_Descriptor(invocation, descriptor),
  MethodLookup(simplename, descriptor, heaptype, toMethod),
  ThisVar(toMethod, this).
# .plan 1:(4,3,2,1,5,6,7,8,9)
InstanceFieldPointsTo(heap, fld, baseheap) :-
  Reachable(inmethod),
  StoreInstanceField(from, base, fld, inmethod),
  VarPointsTo(heap, from),
  VarPointsTo(baseheap, base).
# .plan 1:(3,2,1,4), 2:(4,2,1,3)
StaticFieldPointsTo(heap, fld) :-
  Reachable(inmethod),
  StoreStaticField(from, fld, inmethod),
  VarPointsTo(heap, from).

# Reachable(toMethod), CallGraphEdge(invocation, toMethod)
Reachable(toMethod) :-
  Reachable(inMethod),
  Instruction_Method(invocation, inMethod),
  VirtualMethodInvocation_Base(invocation, base),
  VarPointsTo(heap, base),
  HeapAllocation_Type(heap, heaptype),
  VirtualMethodInvocation_SimpleName(invocation, simplename),
  VirtualMethodInvocation_Descriptor(invocation, descriptor),
  MethodLookup(simplename, descriptor, heaptype, toMethod).
CallGraphEdge(invocation, toMethod) :-
  Reachable(inMethod),
  Instruction_Method(invocation, inMethod),
  VirtualMethodInvocation_Base(invocation, base),
  VarPointsTo(heap, base),
  HeapAllocation_Type(heap, heaptype),
  VirtualMethodInvocation_SimpleName(invocation, simplename),
  VirtualMethodInvocation_Descriptor(invocation, descriptor),
  MethodLookup(simplename, descriptor, heaptype, toMethod).
# .plan 1:(4,3,2,1,5,6,7,8)

# Reachable(tomethod), CallGraphEdge(invocation, tomethod)
Reachable(tomethod) :-
  Reachable(inmethod),
  StaticMethodInvocation(invocation, tomethod, inmethod).
CallGraphEdge(invocation, tomethod) :-
  Reachable(inmethod),
  StaticMethodInvocation(invocation, tomethod, inmethod).

# Reachable(tomethod), CallGraphEdge(invocation, tomethod), VarPointsTo(heap, this)
Reachable(tomethod) :-
  Reachable(inmethod),
  Instruction_Method(invocation, inmethod),
  SpecialMethodInvocation_Base(invocation, base),
  VarPointsTo(heap, base),
  MethodInvocation_Method(invocation, tomethod),
  ThisVar(tomethod, this).
CallGraphEdge(invocation, tomethod) :-
  Reachable(inmethod),
  Instruction_Method(invocation, inmethod),
  SpecialMethodInvocation_Base(invocation, base),
  VarPointsTo(heap, base),
  MethodInvocation_Method(invocation, tomethod),
  ThisVar(tomethod, this).
VarPointsTo(heap, this) :-
  Reachable(inmethod),
  Instruction_Method(invocation, inmethod),
  SpecialMethodInvocation_Base(invocation, base),
  VarPointsTo(heap, base),
  MethodInvocation_Method(invocation, tomethod),
  ThisVar(tomethod, this).
# .plan 1:(4,3,2,1,5,6)

Reachable(method) :-
  MainMethodDeclaration(method).





# ./target/release/executing -p /users/hangdong/xalan/xalan.dl -f /users/hangdong/xalan -c ./examples/csvs -d $'\t' -w 8

